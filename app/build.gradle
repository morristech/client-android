apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def conf = rootProject.ext.config

android {

    compileSdkVersion conf.compileSdkVersion
    buildToolsVersion conf.buildToolsVersion

    defaultConfig {

        applicationId conf.applicationId
        versionCode conf.versionCode
        versionName conf.versionName

        minSdkVersion conf.minSdkVersion
        targetSdkVersion conf.targetSdkVersion

        testInstrumentationRunner conf.testInstrumentationRunner
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true

        def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        resValue "string", "mixpanel_key", "your_key_goes_here"

        resValue "string", "weather_api_key", "your_key_goes_here"

        def config = new Properties()
        config.load(new FileInputStream(rootProject.file("config/secret.properties")))

        resValue "string", "base_url", config.serverUrl
        resValue "string", "api_auth_user", config.apiAuthUser
        resValue "string", "api_auth_key", config.apiAuthKey
    }

    buildTypes {

        debug {
            debuggable true
            applicationIdSuffix ".debug"
            buildConfigField "String", "GIT_SHA", "\"DUMMY_GIT_SHA\""
        }

        release {
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

androidExtensions {
    experimental = true
}

dependencies {

    implementation project(':domain')
    implementation project(':data')

    /* View */
    implementation Deps.appCompat
    implementation Deps.support_v4
    implementation Deps.recyclerview_v7
    implementation Deps.cardview_v7
    implementation Deps.support_annotations
    implementation Deps.design
    implementation("com.mikepenz:materialdrawer:6.0.2@aar") { transitive = true }
    implementation Deps.bottom_navigation

    /* Core */
    implementation Deps.rxrelay
    implementation Deps.dagger
    kapt 'com.google.dagger:dagger-compiler:2.12'
    implementation Deps.core_ktx
    implementation Deps.multidex
    implementation Deps.picasso
    implementation Deps.picasso2_okhttp3_downloader

    /* Debug */
    implementation Deps.leakcanary_android
    implementation Deps.stetho
    implementation Deps.stetho_okhttp3

    /* Analytics */
    implementation Deps.mixpanel_android

    /* Various */
    implementation Deps.reactivenetwork_rx2
    implementation Deps.kotlin_stdLib

    /* Testing */
    androidTestCompile Deps.junit
    androidTestCompile Deps.mockito_core
    androidTestCompile Deps.runner

    androidTestCompile Deps.dagger
    kaptTest Deps.dagger_compiler
    kaptAndroidTest Deps.dagger_compiler

    androidTestCompile Deps.test_rules
    androidTestCompile Deps.espresso_core

    androidTestCompile Deps.dexmaker
    androidTestCompile Deps.dexmaker_mockito

}
repositories {
    mavenCentral()
}
